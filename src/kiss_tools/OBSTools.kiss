(var &mut :String projectObsFolder null)
(var &mut :Bool obsIsRecording false)
(var &mut :sys.io.Process obsProcess null)

(function obsExe []
    (case (Sys.systemName)
        ("Linux" "obs")
        ("Windows" #"C:\Program Files\obs-studio\bin\64bit\obs64.exe"#)
        (otherwise (throw "OBS executable not defined for $(Sys.systemName)"))))

(function :Void startObs []
    (when obsIsRecording
        (print "Warning! OBS is already recording!")
        (return))
    (if projectObsFolder
            (let [os (Sys.systemName)
                    exe (obsExe)
                    dirOfExe (haxe.io.Path.directory exe)
                    profileDir "${projectObsFolder}/${os}_Profile"
                    sceneCollectionFile "${projectObsFolder}/${os}_SceneCollection.json"]
                (if (and (sys.FileSystem.exists profileDir) (sys.FileSystem.exists sceneCollectionFile))
                        (when (Prelude.tryProcess exe ["--version"]
                                    ->error {
                                        (print "error trying to run OBS: $error")
                                    }
                                    []
                                    true
                                    (when dirOfExe dirOfExe))
                            
                            // This is required to find the local.ini file:
                            (localVar lastCwd (Sys.getCwd))
                            (when dirOfExe (Sys.setCwd dirOfExe))
                            (set obsProcess
                                (new sys.io.Process
                                    exe
                                    [
                                        "--profile"
                                        profileDir
                                        "--collection"
                                        sceneCollectionFile
                                        "--scene"
                                        "Scene"
                                        "--startrecording"
                                        "--minimize-to-tray"
                                        "--multi"
                                    ]))
                            (when dirOfExe (Sys.setCwd lastCwd))
                            (set obsIsRecording true))
                    (print "OBS profile and scene collection not found for ${os} in ${projectObsFolder}. Will not be recording")))
        (print "OBSTools.projectObsFolder is not set. Will not be recording")))

(function :Void stopObs []
    (#when debug
        (when obsIsRecording
            // kill the obs process
            (obsProcess.kill)
            (set obsIsRecording false))))

